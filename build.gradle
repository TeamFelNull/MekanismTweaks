buildscript {
    repositories {
        jcenter()
        maven {
            name "forge"
            url "https://files.minecraftforge.net/maven"
        }

        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

version = mod_version
group = mod_group
archivesBaseName = "$mod_id-$minecraft_version"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = mappings_version
}

repositories {
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/maven" }


    maven {
        url "https://cursemaven.com"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }

    maven {
        name 'ForgeFS'
        url 'https://maven.minecraftforge.net/'
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {

    embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }

    compile 'curse.maven:Mekanism-268560:2835175'
    runtime 'mezz.jei:jei_1.12.2:4.16.1.302'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'mod_id': mod_id, 'mod_name': mod_name, 'version': project.version,
                'mcversion': project.minecraft.version, 'mod_description': mod_description,
                'mod_author': mod_author
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, mod_mixin_refmap.replace('${mod_id}', mod_id)
}

jar {
    from(configurations.embed.collect {
        it.isDirectory() ? it : zipTree(it)
    }) {
        exclude "LICENSE.txt", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }

    manifest {
        attributes(
                "FMLCorePlugin": mod_core_plugin.replace('${mod_group}', mod_group),
                "FMLCorePluginContainsFMLMod": "true",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs": mod_mixin_configs.replace('${mod_id}', mod_id),

                // ここでModに必要な情報を追加
                "ModID": mod_id, // modidとしてmodの名前を使用
                "ModName": mod_name, // モッド名
                "Version": mod_version, // モッドのバージョン
                "Description": mod_description
        )
    }
}
